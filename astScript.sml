
open HolKernel Parse boolLib bossLib term_tactic;
open arithmeticTheory listTheory stringTheory alistTheory optionTheory
     ltreeTheory llistTheory bagTheory quotient_llistTheory;

val _ = new_theory "ast";

(* AST for a small functional language *)

Type vname = ‚Äú:string‚Äù  (* variable name *)
Type fname = ‚Äú:string‚Äù  (* function name *)

(*configuration record for the parametric atoms.

   parAtomOp:
     It takes an element of type 'a (from AtomOp) and returns a
     function that takes a "'b list" element and SOME b if the
     number of arguments is correct, NONE otherwise

*)
Datatype:
  conf = <| parAtomOp  : 'a -> 'b list -> 'b option; |>
End

Datatype:
  op = If               (* if-expression                            *)
     | Cons string      (* datatype constructor                     *)
     | IsEq string num  (* compare cons tag and num of args         *)
     | Proj string num  (* reading a field of a constructor         *)
     | AtomOp 'a        (* primitive parametric operator over Atoms *)
     | Lit 'b           (* parametric literal Atom                  *)
End

Datatype:
  exp = Var vname                     (* variable                   *)
      | Prim (('a,'b) op) (exp list)  (* primitive operations       *)
      | App exp exp                   (* function application       *)
      | Lam vname exp                 (* lambda                     *)
      | Letrec ((fname # vname # exp) list) exp   (* mut. rec. funs *)
      | Case exp vname ((vname # vname list # exp) list) (*case pat.*)
End

(* some abbreviations *)
Overload Let  = ‚ÄúŒªs x y. App (Lam s y) x‚Äù      (* let-expression    *)
Overload If   = ‚ÄúŒªx y z. Prim If [x; y; z]‚Äù    (* If   at exp level *)
Overload Lit  = ‚ÄúŒªa. Prim (Lit a) []‚Äù           (* Lit at exp level *)
Overload Cons = ‚ÄúŒªs. Prim (Cons s)‚Äù            (* Cons at exp level *)
Overload IsEq = ‚ÄúŒªs n x. Prim (IsEq s n) [x]‚Äù  (* IsEq at exp level *)
Overload Proj = ‚ÄúŒªs i x. Prim (Proj s i) [x]‚Äù  (* Proj at exp level *)
Overload Fail = ‚ÄúPrim (Lit ARB) [Prim (Lit ARB)[]]‚Äù (* causes Error *)

(* a call-by-name semantics in a denotational semantics style *)

(* would like to have:
Codatatype:
  ('a,'b) v = Atom 'b
          | Constructor string (('a,'b) v) list)
          | Closure vname ('a exp)
          | Diverge
          | Error
End
*)

Datatype:
  v_prefix = Atom' 'b
           | Constructor' string
           | Closure' vname (('a,'b) exp)
           | Diverge'
           | Error'
End

Definition v_rep_ok_def:
  v_rep_ok v ‚áî
    ‚àÄa ts.
      Branch a ts ‚àà subtrees v ‚áí
        case a of
          Constructor' s => LFINITE ts
        | _ => ts = LNIL
End

Theorem v_inhabited[local]:
  ‚àÉf. v_rep_ok f
Proof
  qexists_tac ‚ÄòBranch Error' LNIL‚Äô
  \\ simp [v_rep_ok_def, subtrees]
QED

val v_tydef = new_type_definition ("v", v_inhabited);

val repabs_fns = define_new_type_bijections
  {name = "v_absrep",
   ABS  = "v_abs",
   REP  = "v_rep",
   tyax = v_tydef};

val v_absrep = CONJUNCT1 repabs_fns;
val v_repabs = CONJUNCT2 repabs_fns;

Theorem v_rep_ok_v_rep[local,simp]:
  ‚àÄv. v_rep_ok (v_rep v)
Proof
  rw [v_repabs, v_absrep]
QED

Theorem v_abs_11[local]:
  v_rep_ok v1 ‚àß v_rep_ok v2 ‚áí
    (v_abs v1 = v_abs v2) = (v1 = v2)
Proof
  rw [v_repabs, EQ_IMP_THM] \\ fs []
QED

Theorem v_rep_11[local]:
  (v_rep v1 = v_rep v2) = (v1 = v2)
Proof
  metis_tac [v_absrep]
QED

(*
 * Define constructors.
 *)

Definition Atom_rep_def:
  Atom_rep = Œªb. Branch (Atom' b) LNIL
End

Definition Constructor_rep_def:
  Constructor_rep = Œªs ts. Branch (Constructor' s) (fromList ts)
End

Definition Closure_rep_def:
  Closure_rep = Œªs x. Branch (Closure' s x) LNIL
End

Definition Diverge_rep_def:
  Diverge_rep = Branch Diverge' LNIL
End

Definition Error_rep_def:
  Error_rep = Branch Error' LNIL
End

Definition Atom_def:
  Atom b = v_abs (Atom_rep b)
End

Definition Constructor_def:
  Constructor s ts = v_abs (Constructor_rep s (MAP v_rep ts))
End

Definition Closure_def:
  Closure s x = v_abs (Closure_rep s x)
End

Definition Diverge_def:
  Diverge = v_abs Diverge_rep
End

Definition Error_def:
  Error = v_abs Error_rep
End

(*
 * TODO: Move to llist?
 *)
Theorem LSET_fromList:
  ‚àÄl. LSET (fromList l) = set l
Proof
  Induct \\ rw [fromList_def]
QED


Theorem v_rep_ok_Atom[local]:
  ‚àÄb. v_rep_ok (Atom_rep b)
Proof
  rw [Atom_rep_def, v_rep_ok_def, subtrees]
QED

Theorem v_rep_ok_Constructor[local]:
  ‚àÄs ts. v_rep_ok (Constructor_rep s (MAP v_rep ts))
Proof
  rw [v_rep_ok_def]
  \\ fs [subtrees, Constructor_rep_def, LFINITE_fromList]
  \\ ‚Äòv_rep_ok x‚Äô
    by fs [LSET_fromList, MEM_MAP]
  \\ fs [v_rep_ok_def]
QED

Theorem v_rep_ok_Closure[local]:
  ‚àÄs x. v_rep_ok (Closure_rep s x)
Proof
  rw [Closure_rep_def, v_rep_ok_def, subtrees]
QED

Theorem v_rep_ok_Diverge[local]:
  v_rep_ok Diverge_rep
Proof
  rw [Diverge_rep_def, v_rep_ok_def, subtrees]
QED

Theorem v_rep_ok_Error[local]:
  v_rep_ok Error_rep
Proof
  rw [Error_rep_def, v_rep_ok_def, subtrees]
QED

(*
 * Prove one-one theorems for constructors.
 *)

Theorem Atom_rep_11[local]:
  ‚àÄx y. Atom_rep x = Atom_rep y ‚áî x = y
Proof
  rw [Atom_rep_def]
QED

Theorem Constructor_rep_11[local]:
  ‚àÄs1 t1 s2 t2.
    Constructor_rep s1 t1 = Constructor_rep s2 t2 ‚áî s1 = s2 ‚àß t1 = t2
Proof
  rw [Constructor_rep_def]
QED

Theorem Closure_rep_11[local]:
  ‚àÄn1 x1 n2 x2.
    Closure_rep n1 x1 = Closure_rep n2 x2 ‚áî n1 = n2 ‚àß x1 = x2
Proof
  rw [Closure_rep_def]
QED

Theorem Atom_11:
  ‚àÄx y. Atom x = Atom y ‚áî x = y
Proof
  rw [Atom_def, v_rep_ok_Atom, Atom_rep_11, v_abs_11]
QED

Theorem Constructor_11:
  ‚àÄs1 t1 s2 t2.
    Constructor s1 t1 = Constructor s2 t2 ‚áî s1 = s2 ‚àß t1 = t2
Proof
  rw [Constructor_def]
  \\ eq_tac \\ strip_tac \\ fs []
  \\ qmatch_asmsub_abbrev_tac ‚Äò_ x1 = _ x2‚Äô
  \\ ‚Äòv_rep_ok x1 ‚àß v_rep_ok x2‚Äô
    by (unabbrev_all_tac \\ rw []
        \\ irule v_rep_ok_Constructor)
  \\ unabbrev_all_tac
  \\ fs [v_abs_11, Constructor_rep_11]
  \\ ‚ÄòINJ v_rep (set t1 ‚à™ set t2) ùïå(:('a,'b) v_prefix ltree)‚Äô
    by simp [pred_setTheory.INJ_DEF, v_rep_11]
  \\ drule INJ_MAP_EQ \\ fs []
QED

Theorem Closure_11:
  ‚àÄn1 x1 n2 x2.
    Closure n1 x1 = Closure n2 x2 ‚áî n1 = n2 ‚àß x1 = x2
Proof
  rw [Closure_def, v_rep_ok_Closure, Closure_rep_11, v_abs_11]
QED

Theorem v_11 = LIST_CONJ [Atom_11, Closure_11, Constructor_11];

(*
 * Prove distinctness for constructors.
 *)

Theorem v_distinct:
  ALL_DISTINCT [Atom b; Closure n x; Constructor s t; Error; Diverge]
Proof
  rw [Atom_def, Closure_def, Constructor_def, Error_def, Diverge_def]
  \\ rw [v_rep_ok_Atom,
         v_rep_ok_Closure,
         v_rep_ok_Constructor,
         v_rep_ok_Diverge,
         v_rep_ok_Error,
         v_abs_11]
  \\ rw [Atom_rep_def,
         Closure_rep_def,
         Constructor_rep_def,
         Diverge_rep_def,
         Error_rep_def]
QED

Theorem v_distinct = SIMP_RULE list_ss [GSYM CONJ_ASSOC] v_distinct;

(*
 * Prove nchotomy and cases for constructors.
 *)

Theorem rep_exists[local]:
  v_rep_ok v ‚áí
    (‚àÉb. v = Atom_rep b) ‚à®
    (‚àÉs t. v = Constructor_rep s t) ‚à®
    (‚àÉn x. v = Closure_rep n x) ‚à®
    v = Diverge_rep ‚à®
    v = Error_rep
Proof
  rw [v_rep_ok_def]
  \\ Cases_on ‚Äòv‚Äô
  \\ pop_assum (qspecl_then [‚Äòa‚Äô, ‚Äòts‚Äô] mp_tac)
  \\ simp [subtrees, Atom_rep_def, Constructor_rep_def, Closure_rep_def,
     Diverge_rep_def, Error_rep_def]
  \\ Cases_on ‚Äòa‚Äô \\ fs []
  \\ metis_tac [to_fromList]
QED

val v_repabs_imp =
  v_repabs |> REWRITE_RULE [EQ_IMP_THM] |> SPEC_ALL |> CONJUNCT1 |> GSYM;

Theorem v_nchotomy:
  ‚àÄv.
    (‚àÉb. v = Atom b) ‚à®
    (‚àÉs t. v = Constructor s t) ‚à®
    (‚àÉn x. v = Closure n x) ‚à®
    v = Diverge ‚à®
    v = Error
Proof
  simp [GSYM v_rep_11, Atom_def, Constructor_def, Closure_def,
        Diverge_def, Error_def]
  \\ gen_tac
  \\ qabbrev_tac ‚Äòx = v_rep v‚Äô
  \\ ‚Äòv_rep_ok x‚Äô by simp [Abbr ‚Äòx‚Äô]
  \\ drule rep_exists
  \\ rw [] \\ fs []
  \\ TRY (metis_tac [v_repabs])
  \\ disj2_tac \\ disj1_tac
  \\ qexists_tac ‚Äòs‚Äô
  \\ qexists_tac ‚ÄòMAP v_abs t‚Äô
  \\ simp [MAP_MAP_o, combinTheory.o_DEF]
  \\ imp_res_tac v_repabs
  \\ pop_assum (once_rewrite_tac o single o GSYM)
  \\ rpt AP_TERM_TAC
  \\ rw[LIST_EQ_REWRITE, EL_MAP]
  \\ irule v_repabs_imp
  \\ fs[v_rep_ok_def, subtrees_def]
  \\ rw[]
  \\ first_x_assum irule
  \\ qexists_tac `x::path`
  \\ fs[ltree_lookup_def, Constructor_rep_def, LNTH_fromList]
QED

Definition v_CASE[nocompute]:
  v_CASE v atom cons clos div err =
    case v_rep v of
      Branch (Atom' b) [||] => atom b
    | Branch (Constructor' s) ts => cons s (MAP v_abs (THE (toList ts)))
    | Branch (Closure' n x) [||] => clos n x
    | Branch Diverge' [||] => div
    | Branch Error' [||] => err
    | _ => ARB
End

Theorem v_CASE[compute]:
  v_CASE (Atom b) atom cons clos div err = atom b ‚àß
  v_CASE (Constructor s t) atom cons clos div err = cons s t ‚àß
  v_CASE (Closure n x) atom cons clos div err = clos n x ‚àß
  v_CASE Diverge atom cons clos div err = div ‚àß
  v_CASE Error atom cons clos div err = err
Proof
  rw [v_CASE, Atom_def, Constructor_def, Closure_def, Diverge_def, Error_def]
  \\ qmatch_goalsub_abbrev_tac ‚Äòv_rep (v_abs xx)‚Äô
  \\ ‚Äòv_rep_ok xx‚Äô
    by rw [Abbr ‚Äòxx‚Äô, v_rep_ok_Atom, v_rep_ok_Constructor, v_rep_ok_Closure,
           v_rep_ok_Diverge, v_rep_ok_Error]
  \\ fs [v_repabs, Abbr ‚Äòxx‚Äô, Atom_rep_def, Constructor_rep_def,
         Closure_rep_def, Diverge_rep_def, Error_rep_def, from_toList,
         MAP_MAP_o, combinTheory.o_DEF, v_absrep]
QED

Theorem v_CASE_eq:
  v_CASE v atom cons clos div err = x ‚áî
    (‚àÉb. v = Atom b ‚àß atom b = x) ‚à®
    (‚àÉs t. v = Constructor s t ‚àß cons s t = x) ‚à®
    (‚àÉn y. v = Closure n y ‚àß clos n y = x) ‚à®
    (v = Diverge ‚àß div = x) ‚à®
    (v = Error ‚àß err = x)
Proof
  qspec_then ‚Äòv‚Äô strip_assume_tac v_nchotomy \\ rw []
  \\ fs [v_CASE, v_11, v_distinct]
QED

(*
 * Register with TypeBase.
 *)

Theorem v_CASE_cong:
  ‚àÄM M' atom cons clos div err atom' cons' clos' div' err'.
    M = M' ‚àß
    (‚àÄb. M' = Atom b ‚áí atom b = atom' b) ‚àß
    (‚àÄs t. M' = Constructor s t ‚áí cons s t = cons' s t) ‚àß
    (‚àÄn x. M' = Closure n x ‚áí clos n x = clos' n x) ‚àß
    (M' = Diverge ‚áí div = div') ‚àß
    (M' = Error ‚áí err = err') ‚áí
      v_CASE M atom cons clos div err = v_CASE M' atom' cons' clos' div' err'
Proof
  rw []
  \\ qspec_then ‚ÄòM‚Äô strip_assume_tac v_nchotomy
  \\ rw [] \\ fs [v_CASE]
QED

Theorem datatype_v:
  DATATYPE ((v
             (Atom : 'b -> ('a, 'b) v)
             (Constructor : string -> ('a, 'b) v list -> ('a, 'b) v)
             (Closure : string -> ('a, 'b) exp -> ('a, 'b) v)
             (Diverge : ('a, 'b) v)
             (Error : ('a, 'b) v)) : bool)
Proof
  rw [boolTheory.DATATYPE_TAG_THM]
QED

(* TODO: move to ltreeTheory *)
Theorem ltree_lookup_APPEND:
  ‚àÄ path1 path2 t.
    ltree_lookup t (path1 ++ path2) =
    OPTION_BIND (ltree_lookup t path1) (Œªsubtree. ltree_lookup subtree path2)
Proof
  Induct >> rw[optionTheory.OPTION_BIND_def] >>
  Cases_on `t` >> fs[ltree_lookup_def] >>
  Cases_on `LNTH h ts` >> fs[optionTheory.OPTION_BIND_def]
QED

Theorem v_rep_ok_ltree_el:
  ‚àÄ vtree subtree.
    v_rep_ok vtree ‚àß
    subtree ‚àà subtrees vtree
  ‚áí v_rep_ok subtree
Proof
  rw[] >> fs[subtrees_def, v_rep_ok_def] >> rw[] >>
  rename1 `ltree_lookup vtree vpath` >> rename1 `ltree_lookup subtree spath` >>
  qspecl_then [`vpath`,`spath`,`vtree`] assume_tac ltree_lookup_APPEND >>
  gvs[optionTheory.OPTION_BIND_def] >>
  first_x_assum irule >>
  goal_assum drule
QED

Theorem v_prefix_ltree_bisimulation:
‚àÄ t1 t2.
  t1 = t2 ‚àß v_rep_ok t1 ‚áî
    ‚àÉR. R t1 t2 ‚àß v_rep_ok t1 ‚àß v_rep_ok t2 ‚àß
      ‚àÄ a1 ts1 a2 ts2.
        R (Branch a1 ts1) (Branch a2 ts2) ‚àß
        v_rep_ok (Branch a1 ts1) ‚àß v_rep_ok (Branch a2 ts2)
      ‚áí a1 = a2 ‚àß
        llist_rel R ts1 ts2
Proof
  rw[] >> eq_tac
  >- (rw[] >> qexists_tac `(=)` >> rw[llist_rel_equality]) >>
  rw[ltree_el_eqv] >> fs[] >>
  ntac 3 (last_x_assum mp_tac) >> qid_spec_tac `t1` >> qid_spec_tac `t2` >>
  Induct_on `path` >> rw[] >> rename1 `R s1 s2` >>
  Cases_on `s1` >> Cases_on `s2`
  >- (rw[ltree_el_def] >> fs[llist_rel_def] >> res_tac) >>
  fs[ltree_el_def] >>
  last_assum drule_all >> strip_tac >>
  gvs[llist_rel_def] >>
  Cases_on `LNTH h ts` >> Cases_on `LNTH h ts'` >> fs[] >>
  imp_res_tac LNTH_NONE_LLENGTH >> gvs[] >>
  imp_res_tac LNTH_LLENGTH_NONE >> gvs[] >>
  first_x_assum irule >> reverse (rw[])
  >- (
    first_x_assum irule >>
    goal_assum drule >> fs[]
    ) >>
  irule v_rep_ok_ltree_el >>
  rename1 `subtree ‚àà _` >>
  rename1 `LNTH _ vtree = SOME subtree` >>
  qexists_tac `Branch a vtree` >> fs[subtrees_def] >>
  qexists_tac `[h]` >> fs[ltree_lookup_def]
QED

fun swap_alpha_beta th = th |> INST_TYPE [alpha |-> beta, beta |-> alpha];

Theorem v_bisimulation:
  ‚àÄv1 v2.
    v1 = v2 ‚áî
    ‚àÉR. R v1 v2 ‚àß
        ‚àÄv3 v4. R v3 v4 ‚áí
          (‚àÉa. v3 = Atom a ‚àß v4 = Atom a) ‚à®
          (‚àÉs vs3 vs4.
            v3 = Constructor s vs3 ‚àß
            v4 = Constructor s vs4 ‚àß
            LIST_REL R vs3 vs4) ‚à®
          (‚àÉs e. v3 = Closure s e ‚àß v4 = Closure s e) ‚à®
          (v3 = Diverge ‚àß v4 = Diverge) ‚à®
          (v3 = Error ‚àß v4 = Error)
Proof
  rw[] >> eq_tac >> rw[]
  >- (qexists_tac `(=)` >> fs[v_nchotomy]) >>
  fs[Atom_def, Constructor_def, Closure_def, Diverge_def, Error_def] >>
  fs[Atom_rep_def, Constructor_rep_def, Closure_rep_def,
     Diverge_rep_def, Error_rep_def] >>
  rw[GSYM v_rep_11] >>
  qspecl_then [`v_rep v1`,`v_rep v2`] assume_tac v_prefix_ltree_bisimulation >>
  fs[] >> pop_assum kall_tac >>
  qexists_tac `Œªv1 v2. R (v_abs v1) (v_abs v2)` >> fs[v_absrep] >>
  rpt gen_tac >> strip_tac >>
  fs[GSYM v_rep_11] >>
  assume_tac (v_rep_ok_Atom |> swap_alpha_beta) >>
  assume_tac v_rep_ok_Constructor >>
  assume_tac v_rep_ok_Closure >>
  assume_tac v_rep_ok_Diverge >>
  assume_tac v_rep_ok_Error >>
  fs[Atom_rep_def, Constructor_rep_def, Closure_rep_def,
     Diverge_rep_def, Error_rep_def] >>
  fs[v_repabs] >>
  first_assum drule >>
  strip_tac >> gvs[] >>
  fs[llist_rel_def, LIST_REL_EL_EQN, GSYM LMAP_fromList,
     LNTH_fromList, EL_MAP, v_absrep]
QED

val _ = TypeBase.export
  [TypeBasePure.mk_datatype_info
    { ax = TypeBasePure.ORIG TRUTH,
      induction = TypeBasePure.ORIG v_bisimulation,
      case_def = v_CASE,
      case_cong = v_CASE_cong,
      case_eq = v_CASE_eq,
      nchotomy = v_nchotomy,
      size = NONE,
      encode = NONE,
      lift = NONE,
      one_one = SOME v_11,
      distinct = SOME v_distinct,
      fields = [],
      accessors = [],
      updates = [],
      destructors = [],
      recognizers = [] } ]

(* v_lookup takes a list of indices and a value, and uses the indices one-by-one
   to recurse into the structure of the value. Note that only the `Constructor`
   node has sub-nodes.
   v_lookup returns the specified node, together with the number of children
   of that node. To achieve this, we define the `v_to_prefix` function, which
   removes child nodes *)

Definition v_to_prefix_def:
  v_to_prefix v = case v_rep v of Branch a ts => a
End

Theorem v_to_prefix:
  (‚àÄa. v_to_prefix (Atom a) = Atom' a : (Œ±,Œ≤) v_prefix) ‚àß
  (‚àÄc vs. v_to_prefix (Constructor c vs) = Constructor' c : (Œ±,Œ≤) v_prefix) ‚àß
  (‚àÄx body. v_to_prefix (Closure x body) = Closure' x body : (Œ±,Œ≤) v_prefix) ‚àß
  v_to_prefix Diverge = Diverge' : (Œ±,Œ≤) v_prefix ‚àß
  v_to_prefix Error = Error' : (Œ±,Œ≤) v_prefix
Proof
  fs[Atom_def, Constructor_def, Closure_def, Diverge_def, Error_def] >>
  assume_tac (v_rep_ok_Atom |> swap_alpha_beta) >>
  assume_tac v_rep_ok_Constructor >>
  assume_tac v_rep_ok_Closure >>
  assume_tac v_rep_ok_Diverge >>
  assume_tac v_rep_ok_Error >>
  fs[Atom_rep_def, Constructor_rep_def, Closure_rep_def,
     Diverge_rep_def, Error_rep_def] >>
  fs[v_to_prefix_def, v_repabs]
QED

Definition v_lookup_def:
  v_lookup path v =
    case ltree_lookup (v_rep v) path of
    | SOME (Branch a ts) => (a, THE (LLENGTH ts))
    | NONE => (Diverge', 0)
End

Theorem v_lookup_alt:
  (‚àÄv. v_lookup [] (v : (Œ±,Œ≤) v) =
    (v_to_prefix v, case v of Constructor c vs => LENGTH vs | _ => 0)) ‚àß
  ‚àÄn path. v_lookup (n::path) (v : (Œ±,Œ≤) v) =
    (case v of
    | Constructor c vs =>
      (case oEL n vs of
      | SOME v' => v_lookup path v'
      | NONE => (Diverge', 0))
    | _ => (Diverge', 0))
Proof
  assume_tac (v_rep_ok_Atom |> swap_alpha_beta) >>
  assume_tac v_rep_ok_Constructor >>
  assume_tac v_rep_ok_Closure >>
  assume_tac v_rep_ok_Diverge >>
  assume_tac v_rep_ok_Error >>
  rw[v_lookup_def] >>
  Cases_on `v` >>
  fs[Atom_def, Constructor_def, Closure_def, Diverge_def, Error_def] >>
  fs[v_repabs] >>
  fs[Atom_rep_def, Constructor_rep_def, Closure_rep_def,
     Diverge_rep_def, Error_rep_def] >>
  fs[ltree_lookup_def, v_to_prefix_def] >>
  fs[GSYM LMAP_fromList, LNTH_fromList, oEL_THM] >>
  CASE_TAC >> fs[EL_MAP]
QED

Theorem v_lookup:
  (‚àÄv. v_lookup [] (v : (Œ±,Œ≤) v) =
    case v of
    | Atom a => (Atom' a, 0)
    | Constructor c vs => (Constructor' c, LENGTH vs)
    | Closure x body => (Closure' x body, 0)
    | Diverge => (Diverge', 0)
    | Error => (Error', 0)) ‚àß
  ‚àÄn path. v_lookup (n::path) (v : (Œ±,Œ≤) v) =
    (case v of
    | Constructor c vs =>
      (case oEL n vs of
      | SOME v' => v_lookup path v'
      | NONE => (Diverge', 0))
    | _ => (Diverge', 0))
Proof
  rw[v_lookup_alt] >>
  Cases_on `v` >> rw[v_lookup_alt, v_to_prefix]
QED

Theorem v_lookup_0:
  ‚àÄ path v prefix len.
    v_lookup path v = (prefix, len) ‚àß
    (‚àÄc. prefix ‚â† Constructor' c)
  ‚áí len = 0
Proof
  Induct >> fs[v_lookup] >> rw[] >>
  Cases_on `v` >> fs[] >>
  Cases_on `oEL h t` >> fs[] >>
  first_x_assum irule >>
  goal_assum drule >> goal_assum drule
QED

Theorem v_lookup_Diverge:
  ‚àÄ path. v_lookup path Diverge = (Diverge', 0)
Proof
  Cases >> fs[v_lookup]
QED

(* make_v_rep : (num list -> (Œ±,Œ≤) vprefix # num) -> (Œ±,Œ≤) vprefix ltree *)
(* Given a function which takes a path (:num list) and returns the corresponding
   node, produce the lazy tree of all nodes.
   make_v_rep must also produce ltrees which satisfy v_rep_ok *)
Definition make_v_rep_def:
  make_v_rep f = gen_ltree (
    Œªpath.
      case f path of
      | (Atom' a, _) => (Atom' a, SOME 0)
      | (Constructor' c, n) => (Constructor' c, SOME n)
      | (Closure' x body, _) => (Closure' x body, SOME 0)
      | (Diverge', _) => (Diverge', SOME 0)
      | (Error', _) => (Error', SOME 0))
End

(* TODO move to ltreeTheory *)
Theorem ltree_lookup_SOME_gen_ltree:
  ‚àÄ path f a ts.
    ltree_lookup (gen_ltree f) path = SOME (Branch a ts)
  ‚áí f path = (a, LLENGTH ts)
Proof
  Induct >> rw[]
  >- (
    Cases_on `f []` >> fs[] >>
    gvs[Once gen_ltree, ltree_lookup_def]
    ) >>
  Cases_on `f []` >> fs[] >> rename1 `f [] = (e1, e2)` >>
  gvs[Once gen_ltree, ltree_lookup_def] >>
  fs[LNTH_LGENLIST] >>
  Cases_on `e2` >> gvs[] >>
  Cases_on `h < x` >> fs[]
QED

Triviality v_rep_ok_make_v_rep:
  ‚àÄf. v_rep_ok (make_v_rep f)
Proof
  rw[v_rep_ok_def, subtrees_def, make_v_rep_def] >>
  drule ltree_lookup_SOME_gen_ltree >> rw[] >>
  Cases_on `f path` >> rename1 `(prefix, len_opt)` >> fs[] >>
  Cases_on `prefix` >> gvs[] >>
  fs[LFINITE_LLENGTH] >>
  qexists_tac `len_opt` >> fs[]
QED

(* gen_v : (num list -> (Œ±,Œ≤) vprefix # num) -> (Œ±,Œ≤) v *)
(* Generates a value of type v given a function generating v_prefix nodes when
   given a path *)
Definition gen_v_def:
  gen_v f = v_abs (make_v_rep f)
End

Theorem gen_v:
  ‚àÄf. gen_v f =
    case f [] of
    | (Atom' a, len) => Atom a
    | (Constructor' c, len) =>
        Constructor c (GENLIST (Œªn. gen_v (Œªpath. f (n::path))) len)
    | (Closure' x body, len) => Closure x body
    | (Diverge', len) => Diverge
    | (Error', len) => Error
Proof
  rw[gen_v_def, GSYM v_rep_11] >>
  qspec_then `f` assume_tac v_rep_ok_make_v_rep >>
  fs[v_repabs] >>
  simp[make_v_rep_def, Once gen_ltree] >>
  Cases_on `f []` >> rename1 `f [] = (prefix, len)` >> fs[] >>
  assume_tac (v_rep_ok_Atom |> swap_alpha_beta) >>
  assume_tac v_rep_ok_Constructor >>
  assume_tac v_rep_ok_Closure >>
  assume_tac v_rep_ok_Diverge >>
  assume_tac v_rep_ok_Error >>
  Cases_on `prefix` >>
  fs[Atom_def, Constructor_def, Closure_def, Diverge_def, Error_def] >>
  fs[v_repabs] >>
  fs[Atom_rep_def, Constructor_rep_def, Closure_rep_def,
     Diverge_rep_def, Error_rep_def] >>
  fs[MAP_GENLIST, combinTheory.o_DEF, LGENLIST_EQ_fromList] >>
  rw[GENLIST_FUN_EQ] >>
  qpat_abbrev_tac `tree = gen_ltree _` >>
  rw[GSYM v_repabs] >>
  irule v_rep_ok_ltree_el >>
  qexists_tac `make_v_rep f` >>
  assume_tac v_rep_ok_make_v_rep >> gvs[] >>
  fs[subtrees_def] >>
  qexists_tac `[n]` >>
  rw[make_v_rep_def, Once gen_ltree, ltree_lookup_def, LNTH_LGENLIST]
QED

Theorem gen_v_Atom:
  ‚àÄ f a. gen_v f = Atom a ‚áî ‚àÉr. f [] = (Atom' a, r)
Proof
  rw[] >>
  once_rewrite_tac[gen_v] >>
  CASE_TAC >> CASE_TAC >> fs[]
QED

Theorem gen_v_Constructor_IMP:
  ‚àÄ f c vs. gen_v f = Constructor c vs ‚áí f [] = (Constructor' c, LENGTH vs)
Proof
  rpt gen_tac >>
  simp[Once gen_v] >>
  CASE_TAC >> CASE_TAC >> gvs[] >>
  rpt strip_tac >> rw[LENGTH_GENLIST]
QED

Theorem gen_v_nullary_Constructor:
  ‚àÄ f v.
    gen_v f = Constructor c [] ‚áî f [] = (Constructor' c, 0)
Proof
  rw[] >>
  simp[Once gen_v] >>
  CASE_TAC >> CASE_TAC >> fs[] >>
  eq_tac >> rw[] >>
  Cases_on `r` >> fs[GENLIST]
QED

Theorem gen_v_Closure:
  ‚àÄ f x body. gen_v f = Closure x body ‚áî ‚àÉr. f [] = (Closure' x body, r)
Proof
  rw[] >>
  simp[Once gen_v] >>
  CASE_TAC >> CASE_TAC >> fs[]
QED

Theorem gen_v_Diverge:
  ‚àÄ f. gen_v f = Diverge ‚áî ‚àÉr. f [] = (Diverge', r)
Proof
  rw[] >>
  once_rewrite_tac[gen_v] >>
  CASE_TAC >> CASE_TAC >> fs[]
QED

Theorem gen_v_Error:
  ‚àÄ f. gen_v f = Error ‚áî ‚àÉr. f [] = (Error', r)
Proof
  rw[] >>
  once_rewrite_tac[gen_v] >>
  CASE_TAC >> CASE_TAC >> fs[]
QED



val _ = export_theory ();

