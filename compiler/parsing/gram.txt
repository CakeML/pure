TyBase ::= capIDᵍᵉ | lcIdᵍᵉ
        |  "("ᵍᵉ (Tyᵍᵉ (","ᵍᵉ Tyᵍᵉ)꙳)? ")"ᵍᵉ
        | "["ᵍᵉ Tyᵍᵉ "]"ᵍᵉ
TyApp  ::= (TyBaseᵍᵉ)⁺
Ty     ::= TyAppᵍᵉ ("->"ᵍᵉ TyAppᵍᵉ)꙳

TyConDecl     ::= capIdᵍᵉ (TyBaseᵍᵉ)꙳

Decl ::= lcId⁼ "::"ᵍᵗ Tyᵍᵗ
      |  "data"⁼ capIDᵍᵗ (lcIdᵍᵗ)꙳ "="ᵍᵗ TyConDeclᵍᵗ ("|"ᵍᵗ TyConDeclᵍᵗ)꙳
      |  Exp⁼ "="ᵍᵗ Expᵍᵗ
Decls ::= (Decl⁼)꙳

APat ::= lcId⁼ | Lit⁼

Exp ::= "\"ᵍᵉ (APatᵍᵉ)⁺ "->"ᵍᵉ Expᵍᵉ
     |  "if"ᵍᵉ Expᵍᵉ "then"ᵍᵉ Expᵍᵉ "else"ᵍᵉ Expᵍᵉ
     |  IExpᵍᵉ

IExp ::= FExpᵍᵉ (Opᵍᵉ FExpᵍᵉ)꙳
Op   ::= "$"⁼ | "$!"⁼ | "`seq`"⁼ | "+"⁼ | "*"⁼ | ":"⁼

FExp ::= AExpᵍᵉ (AExpᵍᵉ)꙳
AExp ::= Lit⁼
      | "("⁼ (Expᵍᵉ (","ᵍᵉ Expᵍᵉ)꙳)? ")"ᵍᵉ
      | "["⁼ (Expᵍᵉ (","ᵍᵉ Expᵍᵉ)꙳)? "]"ᵍᵉ
      | lcId⁼
