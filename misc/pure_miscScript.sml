
open HolKernel Parse boolLib bossLib term_tactic BasicProvers;
open stringTheory optionTheory pairTheory listTheory alistTheory llistTheory
     finite_mapTheory pred_setTheory arithmeticTheory rich_listTheory
     ltreeTheory fixedPointTheory

val _ = new_theory "pure_misc";


(******************** Finite maps ********************)

Theorem FDIFF_FUNION:
  âˆ€fm1 fm2 s. FDIFF (fm1 âŠŒ fm2) s = (FDIFF fm1 s) âŠŒ (FDIFF fm2 s)
Proof
  rw[FDIFF_def, DRESTRICTED_FUNION] >>
  rw[fmap_eq_flookup] >>
  rw[FLOOKUP_DRESTRICT, FLOOKUP_FUNION] >> fs[] >>
  rw[FLOOKUP_DEF]
QED

Theorem BIGUNION_DIFF:
  âˆ€as b. (BIGUNION as) DIFF b = BIGUNION {a DIFF b | a âˆˆ as}
Proof
  rw[EXTENSION] >> eq_tac >> rw[] >> gvs[]
  >- (
    qexists_tac `s DIFF b` >> fs[] >>
    goal_assum (drule_at Any) >> fs[]
    )
  >- (
    goal_assum (drule_at Any) >> fs[]
    )
QED

Theorem FDIFF_MAP_KEYS_BIJ:
  BIJ f ð•Œ(:Î±) ð•Œ(:Î²) â‡’
  FDIFF (MAP_KEYS f fm) (IMAGE f s) = MAP_KEYS f (FDIFF fm s)
Proof
  rpt strip_tac >>
  simp[FDIFF_def] >>
  â€˜COMPL(IMAGE f s) = IMAGE f (COMPL s)â€™
    by(rw[COMPL_DEF,IMAGE_DEF,SET_EQ_SUBSET,SUBSET_DEF] >>
       gvs[BIJ_DEF,INJ_DEF,SURJ_DEF] >> metis_tac[]) >>
  pop_assum SUBST_ALL_TAC >>
  gvs[BIJ_DEF] >>
  simp[DRESTRICT_MAP_KEYS_IMAGE]
QED

Theorem DISJOINT_DRESTRICT_FEMPTY:
  âˆ€m s. DISJOINT s (FDOM m) â‡’ DRESTRICT m s = FEMPTY
Proof
  Induct >> rw[]
QED

Theorem fdiff_fdomsub_commute:
  FDIFF (f \\ x) p = FDIFF f p \\ x
Proof
  rw[fmap_eq_flookup,FDIFF_def,FLOOKUP_DRESTRICT,DOMSUB_FLOOKUP_THM] >> rw[]
QED

Theorem fdiff_fdomsub_INSERT:
  FDIFF (f \\ x) p = FDIFF f (x INSERT p)
Proof
  rw[fmap_eq_flookup,FDIFF_def,FLOOKUP_DRESTRICT,DOMSUB_FLOOKUP_THM] >> rw[] >> gvs[]
QED

Theorem fdiff_bound:
  FDIFF f p = FDIFF f (p âˆ© FDOM f)
Proof
  rw[FDIFF_def,fmap_eq_flookup,FLOOKUP_DRESTRICT] >>
  rw[] >> gvs[flookup_thm]
QED

Theorem FLOOKUP_FMAP_MAP2:
  âˆ€f m k. FLOOKUP (FMAP_MAP2 f m) k = OPTION_MAP (Î»v. f (k,v)) (FLOOKUP m k)
Proof
  rw[FLOOKUP_DEF, FMAP_MAP2_def, FUN_FMAP_DEF]
QED

Theorem DOMSUB_FMAP_MAP2:
  âˆ€f m s. (FMAP_MAP2 f m) \\ s = FMAP_MAP2 f (m \\ s)
Proof
  rw[fmap_eq_flookup, DOMSUB_FLOOKUP_THM, FLOOKUP_FMAP_MAP2] >>
  IF_CASES_TAC >> simp[]
QED

Theorem fmap_rel_fupdate_list_MAP_FST:
  âˆ€R f1 f2 l1 l2.
    fmap_rel R (f1 |++ l1) (f2 |++ l2)
  â‡’ set (MAP FST l1) âˆª FDOM f1 = set (MAP FST l2) âˆª FDOM f2
Proof
  rw[fmap_rel_def, FDOM_FUPDATE_LIST] >>
  gvs[EXTENSION] >> metis_tac[]
QED

(* This exists in finite_mapTheory, but with types unnecessarily specialised *)
Theorem fmap_rel_FEMPTY2:
  (fmap_rel (R : 'a -> 'b -> bool) FEMPTY (f1 : 'c |-> 'b) â‡” f1 = FEMPTY) âˆ§
  (fmap_rel R (f2 : 'c |-> 'a) FEMPTY â‡” f2 = FEMPTY)
Proof
  rw[fmap_rel_def] >> simp[FDOM_EQ_EMPTY] >> eq_tac >> rw[]
QED

Theorem fmap_rel_FUPDATE_I_rewrite:
  fmap_rel R (f1 \\ k) (f2 \\ k) âˆ§ R v1 v2 â‡”
  fmap_rel R (f1 |+ (k,v1)) (f2 |+ (k,v2))
Proof
  gvs[fmap_rel_OPTREL_FLOOKUP, FLOOKUP_UPDATE, DOMSUB_FLOOKUP_THM] >>
  reverse (eq_tac >> rw[])
  >- (pop_assum (qspec_then `k` mp_tac) >> simp[]) >>
  first_x_assum (qspec_then `k'` mp_tac) >> simp[] >>
  EVERY_CASE_TAC >> gvs[]
QED

Theorem fmap_rel_ind:
  âˆ€R FR.
    FR FEMPTY FEMPTY âˆ§
    (âˆ€k v1 v2 f1 f2.
      R v1 v2 âˆ§ FR (f1 \\ k) (f2 \\ k) â‡’ FR (f1 |+ (k,v1)) (f2 |+ (k,v2)))
  â‡’ âˆ€f1 f2. fmap_rel R f1 f2 â‡’ FR f1 f2
Proof
  rpt gen_tac >> strip_tac >>
  Induct >> rw[] >- gvs[fmap_rel_FEMPTY2] >>
  `x âˆˆ FDOM f2` by gvs[fmap_rel_def] >>
  imp_res_tac FM_PULL_APART >> gvs[] >>
  last_x_assum irule >> gvs[DOMSUB_NOT_IN_DOM] >>
  gvs[GSYM fmap_rel_FUPDATE_I_rewrite] >>
  first_x_assum irule >>
  gvs[DOMSUB_NOT_IN_DOM]
QED

Theorem FDIFF_FDIFF:
  âˆ€fm s1 s2. FDIFF (FDIFF fm s1) s2 = FDIFF fm (s1 âˆª s2)
Proof
  rw[FDIFF_def, DRESTRICT_DRESTRICT, fmap_eq_flookup, FLOOKUP_DRESTRICT]
QED

Theorem DOMSUB_FUPDATE_LIST:
  âˆ€l m x. (m |++ l) \\ x = (m \\ x) |++ (FILTER ($<> x o FST) l)
Proof
  Induct >> rw[FUPDATE_LIST_THM, fmap_eq_flookup] >>
  PairCases_on `h` >> gvs[] >>
  gvs[flookup_fupdate_list, DOMSUB_FLOOKUP_THM, FLOOKUP_UPDATE] >>
  CASE_TAC >> gvs[]
QED

Theorem FLOOKUP_FDIFF:
  FLOOKUP (FDIFF fm s) k = if k âˆˆ s then NONE else FLOOKUP fm k
Proof
  rw[FDIFF_def, FLOOKUP_DRESTRICT] >> gvs[]
QED

Theorem FDIFF_FUPDATE:
  FDIFF (fm |+ (k,v)) s =
  if k âˆˆ s then FDIFF fm s else (FDIFF fm s) |+ (k,v)
Proof
  rw[fmap_eq_flookup, FLOOKUP_FDIFF, FLOOKUP_UPDATE] >>
  EVERY_CASE_TAC >> gvs[]
QED

Theorem FDIFF_FEMPTY[simp]:
  FDIFF FEMPTY s = FEMPTY
Proof
  rw[fmap_eq_flookup, FLOOKUP_FDIFF]
QED


(******************** Functions/Pairs ********************)

Theorem PAIR_MAP_ALT:
  âˆ€f g. (f ## g) = Î»(x,y). f x, g y
Proof
  rw[] >> irule EQ_EXT >> rw[] >>
  PairCases_on `x` >> gvs[]
QED

Theorem FST_THM:
  FST = Î»(x,y). x
Proof
  irule EQ_EXT >> Cases >> simp[]
QED

Theorem SND_THM:
  SND = Î»(x,y). y
Proof
  irule EQ_EXT >> rw[] >>
  PairCases_on `x` >> rw[]
QED

Theorem CURRY_thm:
  CURRY f = Î» x y. f(x,y)
Proof
  rw[FUN_EQ_THM]
QED

Theorem I_def:
  I = \x. x
Proof
  rw [combinTheory.I_DEF, combinTheory.S_DEF]
QED


(******************** Lists ********************)

Theorem NIL_iff_NOT_MEM:
  âˆ€l. l = [] â‡” âˆ€x. Â¬MEM x l
Proof
  Induct >> rw[] >>
  qexists_tac `h` >> fs[]
QED

Theorem EVERY2_refl_EQ:
  LIST_REL R ls ls â‡” (âˆ€x. MEM x ls â‡’ R x x)
Proof
  simp[EQ_IMP_THM,EVERY2_refl] >>
  Induct_on â€˜lsâ€™ >> rw[] >>
  metis_tac[]
QED

Theorem MAP_ID_ON:
  (âˆ€x. MEM x l â‡’ f x = x) â‡’ MAP f l = l
Proof
  Induct_on â€˜lâ€™ >> rw[]
QED

Theorem ALOOKUP_SOME_EL:
  âˆ€l k v. ALOOKUP l k = SOME v â‡’ âˆƒn. n < LENGTH l âˆ§ EL n l = (k,v)
Proof
  Induct >> rw[] >>
  PairCases_on `h` >> gvs[] >>
  FULL_CASE_TAC >> gvs[]
  >- (qexists_tac `0` >> gvs[]) >>
  first_x_assum drule >> strip_tac >>
  qexists_tac `SUC n` >> gvs[]
QED

Theorem ALOOKUP_SOME_EL_2:
  âˆ€l1 l2 k (v:'a).
    ALOOKUP l1 k = SOME v âˆ§
    MAP FST l1 = MAP FST l2
  â‡’ âˆƒv'. ALOOKUP l2 k = SOME (v':'a) âˆ§
      âˆƒn. n < LENGTH l1 âˆ§ EL n l1 = (k,v) âˆ§ EL n l2 = (k,v')
Proof
  Induct >> rw[] >>
  PairCases_on `h` >> gvs[] >>
  FULL_CASE_TAC >> gvs[]
  >- (
    qexists_tac `SND (EL 0 l2)` >>
    Cases_on `l2` >> gvs[] >> PairCases_on `h` >> gvs[] >>
    qexists_tac `0` >> gvs[]
    ) >>
  first_x_assum drule >> Cases_on `l2` >> gvs[] >>
  disch_then (qspec_then `t` assume_tac) >> gvs[] >>
  PairCases_on `h` >> gvs[] >>
  qexists_tac `SUC n` >> gvs[]
QED

Theorem IS_PREFIX_NOT_EQ:
  âˆ€x y.
    IS_PREFIX x y âˆ§
    x â‰  y â‡’
    LENGTH y < LENGTH x
Proof
  rpt strip_tac >>
  spose_not_then strip_assume_tac >>
  gvs[NOT_LESS] >>
  imp_res_tac IS_PREFIX_LENGTH >>
  â€˜LENGTH x = LENGTH yâ€™ by DECIDE_TAC >>
  metis_tac[IS_PREFIX_LENGTH_ANTI]
QED

Theorem REPLICATE_11:
  âˆ€n m e. REPLICATE n e = REPLICATE m e â‡’ n = m
Proof
  Induct >> rw[] >>
  Cases_on `m` >> gvs[] >>
  first_x_assum irule >> goal_assum drule
QED

Theorem LIST_REL_SYM:
  (âˆ€x y. R x y â‡” R y x) â‡’
  âˆ€xs ys. LIST_REL R xs ys â‡” LIST_REL R ys xs
Proof
  strip_tac \\ Induct
  \\ fs [] \\ rw [] \\ eq_tac \\ rw [] \\ fs [] \\ metis_tac []
QED

Theorem LIST_REL_TRANS:
  (âˆ€x y z. R x y âˆ§ R y z â‡’ R x z) â‡’
  âˆ€xs ys zs. LIST_REL R xs ys âˆ§ LIST_REL R ys zs â‡’ LIST_REL R xs zs
Proof
  strip_tac \\ Induct
  \\ fs [] \\ rw [] \\ fs [] \\ rw [] \\ fs [] \\ metis_tac []
QED

Theorem LIST_REL_mono:
  (âˆ€x y. R x y âˆ§ MEM x xs âˆ§ MEM y ys â‡’ R1 x y) ==>
  LIST_REL R xs ys â‡’ LIST_REL R1 xs ys
Proof
  qid_spec_tac â€˜ysâ€™ \\ Induct_on â€˜xsâ€™ \\ fs [] \\ rw []
QED

Theorem MAP_PAIR_MAP:
  MAP FST (MAP (f ## g) l) = MAP f (MAP FST l) âˆ§
  MAP SND (MAP (f ## g) l) = MAP g (MAP SND l)
Proof
  rw[MAP_MAP_o,combinTheory.o_DEF,MAP_EQ_f]
QED

Theorem ALOOKUP_SOME:
  ALOOKUP l k = SOME v â‡’ MEM k (MAP FST l)
Proof
  rw[] >> imp_res_tac ALOOKUP_MEM >> gvs[MEM_MAP] >>
  goal_assum (drule_at Any) >> simp[]
QED

Theorem LIST_REL_imp_OPTREL_ALOOKUP:
  âˆ€(R:'a -> 'a -> bool) l1 l2.
    LIST_REL R (MAP SND l1) (MAP SND l2) âˆ§ MAP FST l1 = MAP FST l2
  â‡’ âˆ€k. OPTREL R (ALOOKUP l1 k) (ALOOKUP l2 k) âˆ§
        OPTREL R (ALOOKUP (REVERSE l1) k) (ALOOKUP (REVERSE l2) k)
Proof
  rw[LIST_REL_EL_EQN]
  >- (
    Cases_on `ALOOKUP l1 k` >> Cases_on `ALOOKUP l2 k` >> gvs[ALOOKUP_NONE] >>
    imp_res_tac ALOOKUP_SOME >> gvs[] >>
    drule ALOOKUP_SOME_EL_2 >>
    disch_then (qspec_then `l1` assume_tac) >> gvs[] >>
    last_x_assum drule >> simp[EL_MAP]
    )
  >- (
    Cases_on `ALOOKUP (REVERSE l1) k` >>
    Cases_on `ALOOKUP (REVERSE l2) k` >> gvs[ALOOKUP_NONE] >>
    imp_res_tac ALOOKUP_SOME >> gvs[MAP_REVERSE] >>
    drule ALOOKUP_SOME_EL_2 >>
    disch_then (qspec_then `REVERSE l1` assume_tac) >> gvs[MAP_REVERSE] >>
    drule (GSYM EL_REVERSE) >>
    qspecl_then [`n`,`l1`] mp_tac (GSYM EL_REVERSE) >> rw[] >> gvs[] >>
    qmatch_asmsub_abbrev_tac `EL k1 _` >>
    `k1 < LENGTH l2` by (unabbrev_all_tac >> DECIDE_TAC) >>
    last_x_assum drule >> simp[EL_MAP]
    )
QED

Theorem ALL_DISTINCT_FLAT_IMP:
  âˆ€l m. ALL_DISTINCT (FLAT l) âˆ§ MEM m l â‡’ ALL_DISTINCT m
Proof
  Induct >> rw[] >> gvs[ALL_DISTINCT_APPEND]
QED

Theorem ALL_DISTINCT_FLAT_IMP_APPEND:
  âˆ€l m1 m2. ALL_DISTINCT (FLAT l) âˆ§ MEM m1 l âˆ§ MEM m2 l âˆ§ m1 â‰  m2
  â‡’ ALL_DISTINCT (m1 ++ m2)
Proof
  Induct >> reverse (rw[])
  >- (last_x_assum irule >> gvs[ALL_DISTINCT_APPEND]) >>
  gvs[ALL_DISTINCT_APPEND] >>
  irule_at Any ALL_DISTINCT_FLAT_IMP >> goal_assum drule >> simp[] >> rw[] >>
  CCONTR_TAC >> gvs[] >> first_x_assum drule >> simp[MEM_FLAT] >>
  goal_assum drule >> simp[]
QED


(******************** Lazy lists ********************)

Theorem LNTH_NONE_LLENGTH:
  âˆ€ n l .
    LNTH n l = NONE
  â‡’ âˆƒ len.
      LLENGTH l = SOME len âˆ§
      len â‰¤ n
Proof
  Induct >> rw[] >>
  Cases_on `l` >> fs[LNTH] >>
  first_x_assum drule >>
  strip_tac >>
  qexists_tac `SUC len` >> fs[]
QED

Theorem LNTH_2:
  âˆ€ n ll. LNTH n ll =
        if n = 0 then LHD ll
        else OPTION_JOIN (OPTION_MAP (LNTH (n-1)) (LTL ll))
Proof
  rw[] \\ fs[LNTH] \\ Cases_on â€˜nâ€™ \\ fs[LNTH]
QED

Theorem LSET_fromList:
  âˆ€l. LSET (fromList l) = set l
Proof
  Induct \\ rw [fromList_def]
QED

Theorem ltree_lookup_APPEND:
  âˆ€ path1 path2 t.
    ltree_lookup t (path1 ++ path2) =
    OPTION_BIND (ltree_lookup t path1) (Î»subtree. ltree_lookup subtree path2)
Proof
  Induct >> rw[optionTheory.OPTION_BIND_def] >>
  Cases_on `t` >> fs[ltree_lookup_def] >>
  Cases_on `LNTH h ts` >> fs[optionTheory.OPTION_BIND_def]
QED

Theorem ltree_lookup_SOME_gen_ltree:
  âˆ€ path f a ts.
    ltree_lookup (gen_ltree f) path = SOME (Branch a ts)
  â‡’ f path = (a, LLENGTH ts)
Proof
  Induct >> rw[]
  >- (
    Cases_on `f []` >> fs[] >>
    gvs[Once gen_ltree, ltree_lookup_def]
    ) >>
  Cases_on `f []` >> fs[] >> rename1 `f [] = (e1, e2)` >>
  gvs[Once gen_ltree, ltree_lookup_def] >>
  fs[LNTH_LGENLIST] >>
  Cases_on `e2` >> gvs[] >>
  Cases_on `h < x` >> fs[]
QED


(******************** Sets ********************)

Theorem EMPTY_iff_NOTIN:
  âˆ€s. s = {} â‡” âˆ€x. x âˆ‰ s
Proof
  rw[] >> eq_tac >> rw[] >>
  once_rewrite_tac[GSYM SUBSET_EMPTY] >>
  once_rewrite_tac[SUBSET_DEF] >> rw[]
QED

Theorem fresh_list:
  âˆ€s. FINITE s â‡’ âˆƒx. x âˆ‰ s:('a list set)
Proof
  metis_tac[GSYM INFINITE_LIST_UNIV,NOT_IN_FINITE]
QED

Theorem DIFF_SUBSET:
  a DIFF b âŠ† c â‡” a âŠ† b âˆª c
Proof
  rw[SUBSET_DEF, EXTENSION] >>
  eq_tac >> rw[] >> metis_tac[]
QED

Theorem UNION_DIFF_DISTRIBUTE:
  âˆ€A B C. A âˆª B DIFF C = (A DIFF C) âˆª (B DIFF C)
Proof
  rw[EXTENSION] >> metis_tac[]
QED

Theorem UNION_DIFF_EMPTY:
  âˆ€A B C. A âˆª B DIFF C = {} â‡’ B DIFF C = {}
Proof
  rw[EXTENSION] >> metis_tac[]
QED

Theorem monotone_compose:
  monotone f âˆ§ monotone g â‡’ monotone(f o g)
Proof
  rw[monotone_def,pred_setTheory.SUBSET_DEF,IN_DEF] >> res_tac >> metis_tac[]
QED

(****************************************)

val _ = export_theory();
